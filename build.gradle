buildscript {
    // version variables
    ext.v_kotlin = "1.3.70"
    ext.v_wala = "1.5.0"
    ext.v_aspectj = "1.9.1"
    ext.v_klaxon = "3.0.11"
    ext.v_junit = "5.3.1"
    ext.v_log4j2 = "2.11.1"
    ext.v_funktionale = "1.2"
    ext.v_commons_csv = "1.6"
    ext.v_commons_io = "2.6"
    ext.v_asm = "7.0"
    ext.v_picocli = "3.9.2"
//    ext.v_kotlinx_coroutines = "1.1.1"
    ext.v_jgrapht = "1.3.0"
    ext.v_jmh = "1.21"
    ext.v_shadow = "5.0.0"
    ext.v_jmh_gradle = "0.4.8"
    ext.v_eclipse_jdt = "3.18.0"

    repositories {
        mavenCentral()
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath("me.champeau.gradle:jmh-gradle-plugin:$v_jmh_gradle")
        classpath("com.github.jengelman.gradle.plugins:shadow:$v_shadow")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:$v_kotlin")
    }
}


group = "ch.uzh.ifi.seal"
version = "1.0-SNAPSHOT"

apply(plugin: "kotlin")
apply(plugin: "application")
apply(plugin: "com.github.johnrengelman.shadow")
apply(plugin: "me.champeau.gradle.jmh")
//applicationDefaultJvmArgs = ["-Xms6G", "-Xmx8G"]

sourceCompatibility = 1.8

mainClassName = "ch.uzh.ifi.seal.bencher.MainKt"

defaultTasks("run")

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:$v_kotlin")
    implementation("org.jetbrains.kotlin:kotlin-reflect:$v_kotlin")
//    implementation(group: "org.jetbrains.kotlinx", name: "kotlinx-coroutines-core", version: v_kotlinx_coroutines)
    implementation("info.picocli:picocli:$v_picocli")
    implementation(group: "com.beust", name: "klaxon", version: v_klaxon)
    implementation("org.funktionale:funktionale-all:$v_funktionale")
    implementation(group: "com.ibm.wala", name: "com.ibm.wala.core", version: v_wala)
    implementation(group: "org.apache.logging.log4j", name: "log4j-api", version: v_log4j2)
    implementation(group: "org.apache.logging.log4j", name: "log4j-core", version: v_log4j2)
    implementation(group: "org.ow2.asm", name: "asm", version: v_asm)
    implementation(group: "org.apache.commons", name: "commons-csv", version: v_commons_csv)
    implementation(group: "commons-io", name: "commons-io", version: v_commons_io)
    implementation(group: "org.jgrapht", name: "jgrapht-core", version: v_jgrapht)
    implementation(group: "org.openjdk.jmh", name: "jmh-core", version: v_jmh)
    implementation(group: 'org.eclipse.jdt', name: 'org.eclipse.jdt.core', version: v_eclipse_jdt)
    jmh(group: "org.openjdk.jmh", name: "jmh-generator-annprocess", version: v_jmh)

    testImplementation(
            "org.junit.jupiter:junit-jupiter-api:$v_junit",
            "org.junit.jupiter:junit-jupiter-params:$v_junit"
    )
    testRuntimeOnly(
            "org.junit.jupiter:junit-jupiter-engine:$v_junit"
    )
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

run {
    def clargs = System.getProperty("args")
    if (clargs != null) {
        if (clargs.contains("\"")) {
            args(splitWith("\"", clargs))
        } else if (clargs.contains("\'")) {
            args(splitWith("\'", clargs))
        } else {
            args(clargs.split(" "))
        }
    }
}

def splitWith(c, clargs) {
    def inC = false
    def ret = []
    def curr = ""

    clargs.each { i ->
        if (i == c) {
            inC = !inC
        } else if (i == " " && !inC) {
            ret.add(curr)
            curr = ""
        } else {
            curr += i
        }
    }

    if (curr != "") {
        ret.add(curr)
    }

    return ret
}

test {
    useJUnitPlatform()
    testLogging {
        afterSuite { desc, result ->
            if (!desc.parent) { // will match the outermost suite
                println("Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)")
            }
        }
    }
}

jmhJar {
    duplicatesStrategy = DuplicatesStrategy.WARN

    // set jmh jar name
    //[archiveBaseName]-[archiveAppendix]-[archiveVersion]-[archiveClassifier].[archiveExtension]
    archiveClassifier = "jmh"
}

jmh {
    jmhVersion = v_jmh
    duplicateClassesStrategy = "exclude"
}
